apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


def gitGitVersionCode() {
    return 'git rev-list HEAD --first-parent --count'.execute().text.trim().toInteger()
}

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 26
    defaultConfig {
        applicationId "com.onezero.launcher.launcher"
        minSdkVersion 20
        targetSdkVersion 21
        versionCode gitGitVersionCode()
        versionName "arm_v"+versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            android.applicationVariants.all {
                variant ->
                    variant.outputs.all {
                        //这里修改apk文件名
                        outputFileName = "10UI.apk"
                    }
            }
        }

        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }


    configurations.all {
        resolutionStrategy {
            force 'com.android.support:support-annotations:27.1.1'
        }
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        abortOnError false
    }
}

ext {
    androidSupportVersion = '26.1.0'
    retrofitVersion = '2.3.0'
    logginginterceptor = '3.9.1'
    rxjavaVersion = '2.1.11'
    rxandroidVersion = "2.0.2"
    eventBusVersion = "3.0.0"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$logginginterceptor"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    implementation 'com.alibaba:fastjson:1.2.49'
}
